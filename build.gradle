plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.sonarqube" version "2.7.1"
}

group = 'io.github.gurv'
version = '0.2.0'

ext {
    bomVersion = '0.2.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
    maven { url 'http://repo.spring.io/plugins-release' }
    maven { url "https://plugins.gradle.org/m2" }
    mavenLocal()
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gurv-github"
        property "sonar.projectVersion", "${project.version}"
        property "sonar.sources", "."
        property "sonar.links.ci", "https://travis-ci.org/gurv/vg-project"
        property "sonar.links.scm", "https://github.com/gurv/vg-project"
        property "sonar.links.issue", "https://github.com/gurv/vg-project/issues"
    }
}

project(':vg-dependencies') {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'
    apply plugin: 'signing'

    ext {
        springBootVersion = '2.1.8.RELEASE'
        springCloudVersion = 'Greenwich.RELEASE'
        springSecurityVersion = '5.1.6.RELEASE'
        springSecurityOAuthVersion = '2.3.6.RELEASE'
        springSecurityJwtVersion = '1.0.10.RELEASE'
        springCloudDataflowVersion = '2.2.1.RELEASE'
        springCloudDataflowServerLocalVersion = '1.7.4.RELEASE'
        springCloudSkipperVersion = '2.1.2.RELEASE'
        springCloudStreamVersion = 'Germantown.RELEASE'
        springfoxVersion = '2.9.2'
        micrometerVersion = '1.2.1'
        groovyVersion = '2.5.8'
        lombookVersion = '1.18.10'
        avroVersion = '1.9.1'
        jaxbVersion = '2.3.1'
        glassfishJaxbVersion = '2.3.2'
    }

    configurations {
        bom
        bomImport
    }

    dependencies {
        bomImport "org.springframework.boot:spring-boot-dependencies:${project.springBootVersion}"
        bomImport "org.springframework.cloud:spring-cloud-dependencies:$project.springCloudVersion"
        bomImport "org.springframework.cloud:spring-cloud-stream-dependencies:$project.springCloudStreamVersion"
        bomImport "org.springframework.cloud:spring-cloud-dataflow-dependencies:$project.springCloudDataflowVersion"
        bomImport "org.springframework.security:spring-security-bom:$project.springSecurityVersion"
        bom "org.springframework.cloud:spring-cloud-skipper-server:$project.springCloudSkipperVersion"
        bom "org.springframework.cloud:spring-cloud-skipper-shell:$project.springCloudSkipperVersion"
        bom "org.springframework.cloud:spring-cloud-starter-dataflow-server-local:$project.springCloudDataflowServerLocalVersion"
        bom "org.springframework.cloud:spring-cloud-dataflow-server-local:$project.springCloudDataflowServerLocalVersion"
        bom "org.springframework.security.oauth:spring-security-oauth2:$project.springSecurityOAuthVersion"
        bom "org.springframework.security:spring-security-jwt:$project.springSecurityJwtVersion"
        bom "io.springfox:springfox-swagger-ui:$project.springfoxVersion"
        bom "io.springfox:springfox-swagger2:$project.springfoxVersion"
        bom "io.micrometer:micrometer-spring-legacy:$project.micrometerVersion"
        bom "io.micrometer:micrometer-registry-prometheus:$project.micrometerVersion"
        bom "org.codehaus.groovy:groovy-all:$project.groovyVersion"
        bom "org.projectlombok:lombok:$project.lombookVersion"
        bom "org.apache.avro:avro:$project.avroVersion"
        bom "javax.xml.bind:jaxb-api:$project.jaxbVersion"
        bom "org.glassfish.jaxb:jaxb-runtime:$project.glassfishJaxbVersion"
    }

    publishing {
        publications {
            bom(MavenPublication) {
                groupId = rootProject.group
                artifactId = project.name
                version = rootProject.bomVersion
                pom {
                    name = 'VG dependencies'
                    description = 'Java dependencies'
                    url = 'https://gurv.github.io/vg-doc/index.html'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'hhttp://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'gurv'
                            name = 'Vladimir Gurinovich'
                            email = 'vladimir.gurinovich@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/gurv/vg-project.git'
                        developerConnection = 'scm:git:ssh://github.com/gurv/vg-project.git'
                        url = 'http://github.com/gurv/vg-project'
                    }
                }
                pom.withXml {
                    def dependencies = asNode().dependencyManagement.dependencies[0]
                    configurations.bomImport.dependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        dependency.appendNode('type', 'pom')
                        dependency.appendNode('scope', 'import')
                    }
                    configurations.bom.dependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'ci'
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = rootProject.bomVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.env.CI_DEPLOY_USERNAME
                    password System.env.CI_DEPLOY_PASSWORD
                }
            }
        }
    }
}